# Build for the Cilkscale runtime support library.

set(CILKSCALE_SOURCES
  cilkscale.cpp
  csanrt.cpp)

set(BENCHMARK_SOURCES
  benchmark.cpp
  csanrt.cpp)

include_directories(${COMPILER_RT_SOURCE_DIR}/include)

set(CILKSCALE_CFLAGS ${SANITIZER_COMMON_CFLAGS})
append_list_if(COMPILER_RT_HAS_STD_CXX11_FLAG -std=c++11 CILKSCALE_CFLAGS)
append_list_if(COMPILER_RT_HAS_CILK_FLAG -fopencilk CILKSCALE_CFLAGS)
append_rtti_flag(OFF CILKSCALE_CFLAGS)

set(CILKSCALE_COMMON_DEFINITIONS)
append_list_if(COMPILER_RT_HAS_CILK_FLAG SERIAL_TOOL=0 CILKSCALE_COMMON_DEFINITIONS)

set(CILKSCALE_DYNAMIC_LINK_FLAGS)
append_list_if(COMPILER_RT_HAS_CILK_FLAG -fopencilk CILKSCALE_DYNAMIC_LINK_FLAGS)

set(CILKSCALE_DYNAMIC_CFLAGS ${CILKSCALE_CFLAGS})
set(CILKSCALE_DYNAMIC_DEFINITIONS ${CILKSCALE_COMMON_DEFINITIONS})

set(CILKSCALE_COMMON_LIBS ${SANITIZER_CXX_ABI_LIBRARY} ${SANITIZER_COMMON_LINK_LIBS})

set(CILKSCALE_DYNAMIC_LIBS ${CILKSCALE_COMMON_LIBS})

set(CILKSCALE_INSTRUCTIONS_COMMON_DEFINITIONS
  ${CILKSCALE_COMMON_DEFINITIONS} CSCALETIMER=INST)
set(CILKSCALE_INSTRUCTIONS_DYNAMIC_DEFINITIONS
  ${CILKSCALE_INSTRUCTIONS_COMMON_DEFINITIONS})

# Build Cilkscale runtimes shipped with Clang.
add_compiler_rt_component(cilkscale)

foreach (arch ${CILKSCALE_SUPPORTED_ARCH})
  add_compiler_rt_runtime(clang_rt.cilkscale
    STATIC
    ARCHS ${arch}
    SOURCES ${CILKSCALE_SOURCES}
    CFLAGS ${CILKSCALE_CFLAGS}
    DEFS ${CILKSCALE_COMMON_DEFINITIONS}
    PARENT_TARGET cilkscale)

  add_compiler_rt_runtime(clang_rt.cilkscale
    SHARED
    ARCHS ${arch}
    SOURCES ${CILKSCALE_SOURCES}
    CFLAGS ${CILKSCALE_DYNAMIC_CFLAGS}
    LINK_FLAGS ${CILKSCALE_DYNAMIC_LINK_FLAGS}
    LINK_LIBS ${CILKSCALE_DYNAMIC_LIBS}
    DEFS ${CILKSCALE_DYNAMIC_DEFINITIONS}
    PARENT_TARGET cilkscale)

  add_compiler_rt_runtime(clang_rt.cilkscale-instructions
    STATIC
    ARCHS ${arch}
    SOURCES ${CILKSCALE_SOURCES}
    CFLAGS ${CILKSCALE_CFLAGS}
    DEFS ${CILKSCALE_INSTRUCTIONS_COMMON_DEFINITIONS}
    PARENT_TARGET cilkscale)

  add_compiler_rt_runtime(clang_rt.cilkscale-instructions
    SHARED
    ARCHS ${arch}
    SOURCES ${CILKSCALE_SOURCES}
    CFLAGS ${CILKSCALE_DYNAMIC_CFLAGS}
    LINK_FLAGS ${CILKSCALE_DYNAMIC_LINK_FLAGS}
    LINK_LIBS ${CILKSCALE_DYNAMIC_LIBS}
    DEFS ${CILKSCALE_INSTRUCTIONS_DYNAMIC_DEFINITIONS}
    PARENT_TARGET cilkscale)

  add_compiler_rt_runtime(clang_rt.cilkscale-benchmark
    STATIC
    ARCHS ${arch}
    SOURCES ${BENCHMARK_SOURCES}
    CFLAGS ${CILKSCALE_CFLAGS}
    DEFS ${CILKSCALE_COMMON_DEFINITIONS}
    PARENT_TARGET cilkscale)

  add_compiler_rt_runtime(clang_rt.cilkscale-benchmark
    SHARED
    ARCHS ${arch}
    SOURCES ${BENCHMARK_SOURCES}
    CFLAGS ${CILKSCALE_DYNAMIC_CFLAGS}
    LINK_FLAGS ${CILKSCALE_DYNAMIC_LINK_FLAGS}
    LINK_LIBS ${CILKSCALE_DYNAMIC_LIBS}
    DEFS ${CILKSCALE_DYNAMIC_DEFINITIONS}
    PARENT_TARGET cilkscale)
endforeach()

if (COMPILER_RT_INCLUDE_TESTS)
  # TODO(bruening): add tests via add_subdirectory(tests)
endif()
